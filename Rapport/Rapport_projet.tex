\documentclass{article}
\usepackage[utf8]{inputenc}
\usepackage[a4paper, margin=2.5cm]{geometry}
\usepackage{graphicx} 
\usepackage{natbib}
\usepackage[french]{babel}

\usepackage[default,scale=0.95]{opensans}
\usepackage[T1]{fontenc}
\usepackage{amssymb} %math
\usepackage{amsmath}
\usepackage{amsthm}
\usepackage{systeme}
\usepackage{bbm}
\usepackage{media9}
\usepackage{makecell}
\usepackage{url}
\usepackage{minted}
\usepackage{xcolor}
% \usepackage{lineno}
% \linenumbers

\usepackage{hyperref}
\hypersetup{
    % colorlinks=true,
    % linkcolor=blue,
    % filecolor=magenta,      
    % urlcolor=cyan,
    pdftitle={Projet LRC},
    %pdfpagemode=FullScreen,
    }
\urlstyle{same} %\href{url}{Text}

\renewcommand{\baselinestretch}{1.5}

\begin{document}

\begin{titlepage}
    \begin{center}
        \vspace*{1cm}

        \Huge
        \textbf{Rapport de projet}

        \vspace{0.5cm}
        \LARGE
        Écriture en Prolog d'un démonstrateur basé sur l'algorithme des tableaux pour la logique de description  $ \mathcal{ALC}$

        \vspace{1.5cm}

        \textbf{Charles Vin}\\
        \textbf{Barthélémy Dang-Nhu}

        \vfill



        \normalsize

        \textbf{Année :}
        2022/2023
        \hfill
        \includegraphics[width=0.25\textwidth]{./src/logo.png}
    \end{center}
\end{titlepage}

\tableofcontents
\newpage

\section{Description générale et fonctionnement}
\subsection{Les fichiers}
Pour une meilleure compréhension du code, nous avons séparer celui-ci en plusieurs fichier. Chacun à sa spécialisation : \begin{itemize}
    \item \verb|T-A_box.pl| : C'est ici que l'utilisateur entre la TBox et la ABox initiale.
    \item \verb|run.pl| : Contient le prédicat \verb|programme/0|  qui appel les grandes étapes nécessaire à la résolution.
    \item \verb|part1.pl| : Contient les fonctions liées à la première partie décrite par le sujet.
    \item \verb|part2.pl| : Contient les fonctions liées à la deuxième partie décrite par le sujet.
    \item \verb|part3.pl| : Contient les fonctions liées à la troisième partie décrite par le sujet.
    \item \verb|helper.pl| : Contient quelques prédicats utiles
\end{itemize}

\subsection{Utilisation du programme}
\subsubsection{Initialisation de la TBox et de la ABox}
Les informations de la TBox et de la ABox sont à saisir dans le fichier \verb|T-A_Box.pl| à la racine du projet. On utilisera les prédicats suivant : \begin{itemize}
    \item \color{blue} equiv(ConceptAtomique, ConceptGénérique) \color{black} pour indiquer les équivalences.
    \item \color{blue} inst(Instance, ConceptGénérique) \color{black} pour indiquer les instaniations de concepts. 
    \item \color{blue} instR(Instance1, Instance2, rôle) \color{black} pour indiquer les instanciations de rôles.
    \item \color{blue} cnamea(ConceptAtomique) \color{black} pour indiquer les identificateur de concepts atomiques?
    \item \color{blue} cnamena(ConceptAtomique) \color{black} pour indiquer les identificateur de concepts non-atomiques.
    \item \color{blue} iname(Instance) \color{black} pour indiquer les identificateurs d'instances
    \item \color{blue} rname(Rôle) \color{black} pour indiquer les rôles
\end{itemize}
Le programme s'arrêtera et l'utilisateur sera avertie en cas d'une erreur de grammaire ou de syntaxe mais également si la Tbox est cyclique.

\subsubsection{Lancement et saisie de la proposition à prouver}
Pour lancer le programme entrez cette commande à la racine du projet : \verb|swipl -f run.pl|. Puis dans l'interpréteur prolog utilisez le prédicat \color{blue} programme/0 \color{black} pour lancer le programme.


\subsection{Exemple d'utilisation}


\section{\'Etape préliminaire de vérification et de mise en forme de la Tbox et de la Abox}
\subsection{Correction syntaxique et sémantique}
Dans cette première partie nous commençons par vérifier la correction sémantique et syntaxique des deux box. Pour ce faire nous implémentons les prédicats d'arité 1 \color{blue}concept, instance, role\color{black}\ qui vérifie si des objets sont de ce type. Les cas de base sont les suivants :

\begin{minted}{prolog}
concept(C) :- cnamea(C).
concept(CG) :- cnamena(CG). 
instance(I) :- iname(I).
role(R) :- rname(R). 
\end{minted}

Le prédicat \color{blue}concept\color{black}\ nécessite de la récursivité à cause des concepts non-atomiques. Nous vérifions la grammaire avec les prédicats suivants :

\begin{minted}{prolog}
concept(not(C)) :- concept(C).
concept(and(C1, C2)) :- concept(C1), concept(C2).
concept(or(C1, C2)) :- concept(C1), concept(C2).
concept(some(R, C)) :- role(R), concept(C).
concept(all(R, C)) :- role(R), concept(C).
\end{minted}

Nous utilisons ces prédicats pour définir le prédicat \color{blue}definition\color{black}\ d'arité 2 qui vérifie si la définition d'une équivalence est juste : il faut que le premier élément soit un concept non-atomique et que le deuxième soit bien la définition d'un concept :
\begin{minted}{prolog}
definition(CA, CG) :- cnamena(CA), concept(CG).
\end{minted}

Grâce à ce prédicat nous pouvons vérifier la Tbox avec \color{blue}verif\_Tbox\color{black}\ qui prend en argument une TBox sous forme d'une liste de d'équivalence et qui vérifie la correction syntaxique et sémantique : 
\begin{minted}{prolog}
verif_Tbox([(CA, CG) | Q]) :- 
    definition(CA, CG), 
    verif_Tbox(Q).
verif_Tbox([]).
\end{minted}
On fait de même avec la Abox en vérifiant l'instanciation des concepts avec \color{blue}instanciationC(I, CG)\color{black}\ et l'instanction des relations avec \color{blue}instanciationR(I1, I2, R)\color{black}.
\subsection{Vérification de l'auto-référencement}
Nous voulons ensuite s'assurer qu'il n'y a pas d'auto-référencement dans la définition des concepts non-atomiques. S'il y en a, au moment de développer les concepts pour n'avoir que des concepts atomiques il y aura une boucle infinie. Nous implémentons le prédicat \color{blue}pautoref(C, Def)\color{black}\ qui prend en argument un concept non-atomique C et la définition de concept Def et qui est vraie ssi C n'est pas présent récursivement dans Def. Le cas de base est le suivant :
\begin{minted}{prolog}
pautoref(C, Def) :- cnamea(Def).
\end{minted}

Nous pouvons ainsi construire le prédicat \color{blue}verif\_Autoref(L)\color{black}\ qui prend en argument la liste des concept non-atomiques et qui vérifie s'il n'y a pas auto-référencement dans leur définition : 
\begin{minted}{prolog}
verif_Autoref([]).
verif_Autoref([C|L]) :-
	equiv(C, Def_C),
	pautoref(C, Def_C),
	verif_Autoref(L).
\end{minted}
L'utilisation d'equiv nous permet de récupérer directement la définition de C, sans avoir à passer une box en argument. En effet les box n'ont juste là pas été altérée, on peut donc récupérer toutes les informations nécessaires directement avec les prédicats  \color{blue}cnamea, cnamena, iname, rname, equiv, inst, instR\color{black}.
\subsection{Mise sous forme}
Une fois que nous sommes sûr qu'il n'y a pas d'auto-référencement on peut développer les concepts non-atomiques. Nous implémentons le prédicat \color{blue}developpe(C,D)\color{black}\ qui est vraie ssi D est le développement de C. Le cas de base est le suivant : 
\begin{minted}{prolog}
developpe(C, C) :- cnamea(C).
\end{minted}
voici un exemple avec le ou logique :
\begin{minted}{prolog}
developpe(or(C1,C2), or(D1,D2)) :- 
	developpe(C1, D1), 
	developpe(C2, D2).
 \end{minted}
On peut ensuite écrire le prédicat \color{blue}transforme(L1,L2)\color{black}\ qui prend en argument deux box L1 et L2 et qui est vraie si et seulement si L2 est la box équivalent à L1 mais dans laquelle les concepts non-atomiques sont développés puis mis sous formes normales négatives :
\begin{minted}{prolog}
transforme([], []).
transforme([(X,C) | L], [(X,D) | M]) :- 
	developpe(C, E),
	nnf(E, D),
	transforme(L, M).
\end{minted}
Ce prédicat \color{blue} transforme / 2 \color{black} combine \color{blue} traitement\_Tbox \color{black} et \color{blue} traitement\_Abox \color{black} mentionnés par le sujet.

\section{Saisie de la proposition à montrer}
Dans la deuxième partie, l'utilisateur peut choisir d'entrer deux types de propositions à démontrer : 
\begin{itemize}
    \item Un proposition de type $ I : C $ qui sera géré par le prédicat \color{blue}acquisition\_prop\_type1 / 3 \color{black}
    \item Un proposition de type $ C1 \sqcap C2 \sqsubseteq \bot  $ qui sera géré par le prédicat \color{blue}acquisition\_prop\_type2 / 3 \color{black}
\end{itemize}

\subsection{Proposition de type 1 : $ I : C $ }
On commence par demander à l'utilisateur d'entrer $ I $ et $ C $ par le biais du prédicat \color{blue}input\_prop\_type1\color{black}\. C'est également ici que l'on vérifie si l'entrée de l'utilisateur est correct syntaxiquement avec le prédicat \color{blue}instanciationC / 2\color{black}.
\begin{minted}{prolog}
input_prop_type1(I, CG) :-
    write('Ajoutons une instance de concept à la ABox :'), nl,
    write('Elle a la forme "I : C"'), nl,
    write('Entrez I :'),nl, 
    read(I),nl,
    write('Entrez C :'),nl, 
    read(CG),
    (instanciationC(I, CG) -> % if 
        write("Input correct")
        ; ( % else
        write('Erreur : I n\'est pas une instance déclarée ou C n\'est pas un concept'), nl,
        write('Veuillez recommencer'), nl
        % input_prop_type1(I, CG) % boucler ne semble pas fonctionner 
        )), nl.
\end{minted}
Dans la suite d'\color{blue} acquisition\_prop\_type1 \color{black}\ on effectue quelques traitements sur $ \neg C $ en remplaçant de manière récursive les identificateurs de concepts complexes par leur définition et en mettant le tout sous forme normale négative (prédicat \color{blue} transforme \color{black}). On peut ensuite ajouter le tout dans la ABox avec \color{blue} concat \color{black}.
\begin{minted}{prolog}
acquisition_prop_type1(Abi,Abi1,Tbox) :- 
    input_prop_type1(I, CG), % User input
    transforme([(I,not(CG))], [(I, CG_dev_nnf)]), % Développement + nnf
    concat(Abi,[(I, CG_dev_nnf)], Abi1), % Ajout de l'input de l'utilisateur dans la ABox
    write("Abi1"), write(Abi1). 
\end{minted}

\subsection{Proposition de type 2 : $ C1 \sqcap C2 \sqsubseteq \bot $ }
Comme précédemment, on commencer par demander à l'utilisateur d'entrer $ C1 $ et $ C2 $ par le biais du prédicat \color{blue} input\_prop\_type2 / 2 \color{black}. Et on vérifie si l'entrée de l'utilisateur est correct syntaxiquement avec le prédicat \color{blue}concept / 2\color{black}.
\begin{minted}{prolog}
input_prop_type2(C1, C2) :-
    write('Ajoutons une proposition de type 2.'), nl,
    write('Entrez C1 :'),nl, 
    read(C1),nl,
    write('Entrez C2 :'),nl, 
    read(C2),
    (concept(and(C1, C2)) -> % if 
        write("Input correct")
        ; ( % else
        write('Erreur : C1 ou C2 n\'est pas un concept déclarée'), nl,
        write('Veuillez recommencer'), nl
        % input_prop_type2(C1, C2) % boucler ne semble pas fonctionner
    )), nl.
\end{minted}
Cette fois-ci il faut générer un nom de concept aléatoire $ Random\_CName $ afin de pouvoir ajouter dans la ABox $ Random\_CName : C1 \sqcap C2 $, c'est ce qui est fait par le biais de \color{blue} genere / 1 \color{black} et \color{blue} transforme / 2 \color{black}. Puis le tout est \color{blue} concat / 3 \color{black} dans la ABox, la liste unaire est mise en premier argument car dans le cas contraire il y aurait un parcourt intégrale de $Abi$ d'après l'implémentation de \color{blue}concat\color{black}.
\begin{minted}{prolog}
acquisition_prop_type2(Abi, Abi1, Tbox) :- 
    input_prop_type2(C1, C2), % User input
    genere(Random_CName),
    transforme([(Random_CName, and(C1, C2))], [(Random_CName, and(C1_dev_nnf, C2_dev_nnf))]), 
    concat([(Random_CName, and(C1_dev_nnf, C2_dev_nnf))], Abi, Abi1), 
    write("Abi1"), write(Abi1).
\end{minted}

\section{Démonstration de la proposition}
Dans cette partie, nous allons démontrer par la méthode des tableaux si la Abox $ Abe $ construite dans la partie précédente, par l'ajout de la négation de l'entrée de l'utilisateur, mène à une contradiction.

\subsection{Prédicat utile à l'implémentation}
Pour faciliter l'implémentation, on commence par extraire chaque type d'assertion de la Abox pour les mettre dans 5 listes grâce au prédicat \color{blue} tri\_Abox / 6 \color{black}
% Ne pas mettre tout le code ? Comme tu as fait avec developpe/2 dans la partie 1 ?
\begin{minted}{prolog}
tri_Abox([], [], [], [], [], []).
tri_Abox([(I, some(R,C)) | L], [(I, some(R,C)) | Lie], Lpt, Li, Lu, Ls) :-
    tri_Abox(L, Lie, Lpt, Li, Lu, Ls).
tri_Abox([(I, all(R,C)) | L], Lie, [(I, all(R,C)) | Lpt], Li, Lu, Ls) :-
    tri_Abox(L, Lie, Lpt, Li, Lu, Ls).
tri_Abox([(I, and(C1,C2)) | L], Lie, Lpt, [(I, and(C1,C2)) | Li], Lu, Ls) :-
    tri_Abox(L, Lie, Lpt, Li, Lu, Ls).
tri_Abox([(I, or(C1,C2)) | L], Lie, Lpt, Li, [(I, or(C1,C2)) | Lu], Ls) :-
    tri_Abox(L, Lie, Lpt, Li, Lu, Ls).
tri_Abox([(I,C)|L], Lie, Lpt, Li, Lu, [(I,C)|Ls]) :-
    cnamea(C),
    tri_Abox(L, Lie, Lpt, Li, Lu, Ls).
tri_Abox([(I,not(C))|L], Lie, Lpt, Li, Lu, [(I,not(C))|Ls]) :-
    cnamea(C),
    tri_Abox(L, Lie, Lpt, Li, Lu, Ls).
\end{minted}

Le deuxième prédicat important pour l'implémentation est \color{blue} evolue / 11 \color{black} qui prends en première paramètre une nouvelle proposition et l'ajoute dans la liste correspondante en vérifiant qu'il n'y est pas déjà. Voici l'exemple avec Lie :
% Hésite pas à plus détailler si tu le sens
\begin{minted}{prolog}
evolue((I, some(R,C)), Lie, Lpt, Li, Lu, Ls, Lie, Lpt, Li, Lu, Ls) :-
    member((I, some(R,C)), Lie).
evolue((I, some(R,C)), Lie, Lpt, Li, Lu, Ls, [(I, some(R,C)) | Lie], Lpt, Li, Lu, Ls):-
    \+ member((I, some(R,C)), Lie).
\end{minted}
Nous avons aussi besoin d'une fonction \color{blue}evolue\_rec\color{black}\ qui est similaire mais qui prend une liste d'instanciation en premier argument et applique \color{blue}evolue\color{black}\ à tous ses éléments.\\


Enfin, le dernier prédicat qui nous sera utile pour la suite est \color{blue} non\_clash / 1 \color{black}\ qui vérifie l'absence de clash dans la liste d'assertion reçu.
\begin{minted}{prolog}
non_clash([]).
non_clash([(I,C) | Ls]) :-
    nnf(not(C), NC),
    \+ member((I, NC), Ls),
    non_clash(Ls).
\end{minted}
L'appel de \color{blue}nnf\color{black}\ permet de transformer un $\neg\neg$C en C et ne change pas un $\neg$C.

Nous avons aussi implémenter divers fonctions d'affichage qui peut représenter des Abox avec une écriture infixe des concepts.
\subsection{Algorithme de résolution}
Nous avons implémenter le prédicat resolution/6 qui prend en argument un ABox triée et qui applique la méthode des tableaux, elle renvoie vraie ssi une feuille ouverte est trouvée. Le cas de base est donc le suivant :
\begin{minted}{prolog}
resolution([], [], [], [], Ls, _):-
	non_clash(Ls).
\end{minted}

Nous allons ensuite définir cette fonction recursivement en utilisant l'ordre de priorité donnée dans le sujet :
\begin{minted}{prolog}
resolution(Lie, Lpt, Li, Lu, Ls, Abr) :-
    non_clash(Ls),
    complete_some(Lie, Lpt, Li, Lu, Ls, Abr).
	
resolution([], Lpt, Li, Lu, Ls, Abr) :-
    non_clash(Ls),
    transformation_and([], Lpt, Li, Lu, Ls, Abr).
	
resolution([], Lpt, [], Lu, Ls, Abr) :-
    non_clash(Ls),
    deduction_all([], Lpt, [], Lu, Ls, Abr).
	
resolution([], [], [], Lu, Ls, Abr):-
	non_clash(Ls),
	transformation_and([], [], [], Lu, Ls, Abr).
\end{minted}

\`A chaque fois nous vérifions la présence de clash avant l'appel du traitement.

\subsubsection{Intersection}
On souhaite traiter une instanciation $I:C_1\sqcap C_2$.
\begin{minted}{prolog}
transformation_and(Lie, Lpt, [(I, and(C1,C2)) | Li], Lu, Ls, Abr) :- 
	write('Utilisation de la règle \u2A05 sur : '),affiche_Abi([(I, and(C1,C2))]),nl,
	evolue_rec([(I,C1),(I,C2)], Lie, Lpt, Li, Lu, Ls,
            Lie1, Lpt1, Li1, Lu1, Ls1),
	affiche_evolution_Abox(Ls, Lie, Lpt, [(I, and(C1,C2)) | Li], Lu, Abr, Ls1, Lie1, Lpt1, Li1, Lu1, Abr),
	resolution(Lie1, Lpt1, Li1, Lu1, Ls1, Abr).
\end{minted}

L'appel de \color{blue}evolur\_rec\color{black}\ permet de rajouter $I:C_1$ et $I:C_2$ à $Lie$ sans faire de doublon ensuite nous traitons la résolution de la ABox modifée.
\subsubsection{Union}
On souhaite traiter une instanciation $I:C_1\sqcup C_2$.
\begin{minted}{prolog}
transformation_or(Lie, Lpt, Li, [(I, or(C1,C2)) | Lu], Ls, Abr) :- 
	write('Utilisation de la règle \u2A06 sur : '),affiche_Abi([(I, or(C1,C2))]),nl,
 
	write('Br1 : avec '),affiche_concept(C1),nl,
	evolue((I, C1), Lie, Lpt, Li, Lu, Ls, Lie1, Lpt1, Li1, Lu1, Ls1),
	affiche_evolution_Abox(Ls, Lie, Lpt, Li, [(I, or(C1,C2)) | Lu], Abr,
            Ls1, Lie1, Lpt1, Li1, Lu1, Abr),
	resolution(Lie1, Lpt1, Li1, Lu1, Ls1, Abr).
	
transformation_or(Lie, Lpt, Li, [(I, or(C1,C2)) | Lu], Ls, Abr) :- 
	write('Utilisation de la règle \u2A06 sur : '),affiche_Abi([(I, or(C1,C2))]),nl,
 
	write('Br2 : avec '),affiche_concept(C2),nl,
	evolue((I, C2),Lie, Lpt, Li, Lu, Ls, Lie2, Lpt2, Li2, Lu2, Ls2),
	affiche_evolution_Abox(Ls, Lie, Lpt, Li, [(I, or(C1,C2)) | Lu], Abr,
            Ls2, Lie2, Lpt2, Li2, Lu2, Abr),
	resolution(Lie2, Lpt2, Li2, Lu2, Ls2, Abr).
\end{minted}

La structure est similaire à l'intersection mais il nous faut maintenant deux implémentations du même prédicat car il y a deux façons de trouver une branche ouerte, elle peut être dans la branche $Br_1$ ou dans la branche $Br_2$. Lors d'un appel de \color{blue}transformation\_or\color{black}, les arguments vont s'unifier à la première clause de Horn et va chercher une feuille ouverte dans le premier arbre, s'il n'en trouve pas l'appel va s'unifier à la deuxième clause et chercher dans le deuxième arbre. \\

C'est le seul cas où les appels de \color{blue}resolution\color{black}\ ne sont pas linéaires car il y a ici une recherche en profondeur.
\subsubsection{Il existe}
On souhaite traiter une instanciation $I_1:\exists R.C$.
\begin{minted}{prolog}
complete_some([(I1,some(R,C)) | Lie], Lpt, Li, Lu, Ls, Abr) :-
	write('Utilisation de la règle \u2203 sur : '),affiche_Abi([(I1, some(R,C))]),nl,
	genere(I2),
	evolue((I2, C), Lie, Lpt, Li, Lu, Ls, Lie1, Lpt1, Li1, Lu1, Ls1),
	affiche_evolution_Abox(Ls, [(I1,some(R,C)) | Lie], Lpt, Li, Lu, Abr,
            Ls1, Lie1, Lpt1, Li1, Lu1, [(I1, I2, R) | Abr]),
	resolution(Lie1, Lpt1, Li1, Lu1, Ls1, [(I1, I2, R) | Abr]).
\end{minted}

L'appel de \color{blue}genere\color{black}\ permet de génerer un nouvelle instance $I_2$, on va ensuite introduire l'instanciation $I_2:C$ dans la Abox grâce à \color{blue}evolue\color{black}. Il suffit maintenant faire la résoulution de cette nouvelle Abox.

\subsubsection{Pour tout}

On souhaite traiter une instanciation $I_1:\forall R.C$.
\begin{minted}{prolog}
deduction_all(Lie, [(I1, all(R, C)) | Lpt], Li, Lu, Ls, Abr) :-
	write('Utilisation de la règle \u2200 sur : '),affiche_Abi([(I1, all(R,C))]),nl,
 
	findall((I2, C),  member((I1, I2, R), Abr),  LC2), 
	evolue_rec(LC2, Lie, Lpt, Li, Lu, Ls, Lie1, Lpt1, Li1, Lu1, Ls1),
	affiche_evolution_Abox(Ls, Lie, [(I1, all(R, C)) | Lpt], Li, Lu, Abr,
            Ls1, Lie1, Lpt1, Li1, Lu1, Abr), 
	resolution(Lie1, Lpt1, Li1, Lu1, Ls1, Abr).
\end{minted}

On souhaite dans un premier temps trouver l'intégralité des $I_2$ tel que $<I_1,I_2>:R$, pour cela nous allons utiliser le prédicat \color{blue}findall\color{black}\ fournit par prolog. Nous parcourons dans l'ABox de relation les $I_2$ tel que $<I_1,I_2>:R\in Abr$ avec l'aide de \color{blue}member\color{black}. Avec cela nous créons une liste $LI_2$ directement sous la forme de $(I_2,C)$. Ensuite il suffit d'ajouter les éléments de $LI_2$ dans l'Abox avec \color{blue}evolue\_rec\color{black}\ et résoudre la nouvelle ABox.\\

Nous n'utilisons pas \color{blue}setof\color{black}\ car ce prédicat échoue si aucun élément n'est trouvé, or nous voulons que le programme continue même si aucun $I_2$ ne convient, $LI_2$ sera juste vide et rien ne sera ajouté à la ABox.
\end{document} 
